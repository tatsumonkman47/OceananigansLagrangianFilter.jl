using Oceananigans.BoundaryConditions: PeriodicBoundaryCondition, FieldBoundaryConditions
using DataStructures: OrderedDict
using Oceananigans.Grids: AbstractGrid
using NCDatasets



const scipy_interpolate = PythonCall.pynew()
const numpy = PythonCall.pynew()
function __init__()
    PythonCall.pycopy!(scipy_interpolate, pyimport("scipy.interpolate"))
    PythonCall.pycopy!(numpy, pyimport("numpy"))
end

"""
    sum_forward_backward_contributions!(config::AbstractConfig)

Combines the output from the forward and backward filter simulations into a
single output file. This function performs the final step of the offline
filter algorithm by summing the contributions from each pass.

The function performs the following steps:
1.  **Initializes the combined file**: A new JLD2 file is created to store
    the final output.
2.  **Copies metadata and unfiltered data**: The file structure, metadata,
    and the original, unfiltered data are copied from the forward output file.
3.  **Sums filtered contributions**: For each filtered variable, the data
    from the backward output file is loaded as a `FieldTimeSeries`. The data
    is then interpolated to match the time steps of the forward simulation,
    and the two datasets are summed and written to the combined output file.

Arguments
=========
- `config`: An instance of `AbstractConfig` containing the file paths and
  variable names.
- `extra_filtered_var_names::Tuple{Vararg{String}}=()`: Optional tuple of additional
  filtered variable names that have been calculated by the filter and also need to be
  combined.
- `extra_filtered_velocity_names::Tuple{Vararg{String}}=()`: Optional tuple of additional
  filtered velocity names that have been calculated by the filter and also need to be
  combined.
- `extra_original_data_names::Tuple{Vararg{String}}=()`: Optional tuple of additional
  original names that have been output and should be copied to the combined output file.
"""
function sum_forward_backward_contributions!(config::AbstractConfig; extra_filtered_var_names::Tuple{Vararg{String}}=(), 
    extra_filtered_velocity_names::Tuple{Vararg{String}}=(), extra_original_data_names::Tuple{Vararg{String}}=())
    # Combine the forward and backward simulations by summing them into a single file

    output_filename = config.output_filename
    forward_output_filename = config.forward_output_filename
    backward_output_filename = config.backward_output_filename
    T = config.T
    velocity_names = config.velocity_names
    var_names_to_filter = config.var_names_to_filter
    map_to_mean = config.map_to_mean
    compute_mean_velocities = config.compute_mean_velocities
    label = config.label
    
    # When offline filtering, we can turn off advection to get Eulerian filtered fields
    if (config isa AbstractOfflineConfig) && config.advection === nothing
        filter_identifier = "_Eulerian_filtered"
    else
        filter_identifier = "_Lagrangian_filtered"
    end

    # List the names of the fields that we will combine
    filtered_var_names = Tuple([var * label * filter_identifier for var in var_names_to_filter])
    if map_to_mean
        filtered_var_names = (Tuple(["xi_" * vel * label for vel in velocity_names])..., filtered_var_names...)
    end

    # There might be some extra filtered variables that the user defined that we should combine too
    filtered_var_names = Tuple(unique((filtered_var_names..., extra_filtered_var_names...)))

    filtered_vel_names = ()
    vel_names_to_filter = ()
    if compute_mean_velocities
        filtered_vel_names = Tuple([vel * label * filter_identifier for vel in velocity_names])
        vel_names_to_filter = velocity_names
    end

    # There might be some extra filtered velocities that the user defined that we should combine too
    filtered_vel_names = Tuple(unique((filtered_vel_names..., extra_filtered_velocity_names...)))

    jldopen(output_filename,"w") do combined_file
        jldopen(forward_output_filename,"r") do forward_file

            # It's possible the file doesn't contain some of the filtered variables - for example the user might have defined their own outputs. 
            # Let's check and only try to copy the variables that exist in the forward file
            forward_file_all_names = keys(forward_file["timeseries"])
            missing_var_names = [var for var in filtered_var_names if !(var in forward_file_all_names)]
            missing_vel_names = [var for var in filtered_vel_names if !(var in forward_file_all_names)]
            
            if length(missing_var_names) > 0
                @warn "The following filtered variable names were not found in the forward output file and will be skipped: $(missing_var_names)"
            end
            if length(missing_vel_names) > 0
                @warn "The following filtered velocity names were not found in the forward output file and will be skipped: $(missing_vel_names)"
            end

            filtered_var_names = Tuple([var for var in filtered_var_names if var in forward_file_all_names])
            filtered_vel_names = Tuple([var for var in filtered_vel_names if var in forward_file_all_names])


            # First copy the forward file metadata and file structure
            if config.output_original_data
                names_to_copy = (var_names_to_filter..., vel_names_to_filter..., filtered_var_names..., filtered_vel_names...)
            else
                names_to_copy = (filtered_var_names..., filtered_vel_names...)
            end

            # There might be some extra variables provided to copy too
            names_to_copy = Tuple(unique((names_to_copy..., extra_original_data_names...)))
            copy_file_metadata!(forward_file, combined_file, names_to_copy)

            forward_iterations = parse.(Int, keys(forward_file["timeseries/t"]))

            # Copy over the unfiltered field data

            if config.output_original_data
                original_data_names = (var_names_to_filter..., vel_names_to_filter..., extra_original_data_names...,"t")
            else
                original_data_names = ("t",extra_original_data_names...)
            end

            
            for var_name in original_data_names
                for iter in forward_iterations
                    combined_file["timeseries/$var_name/$iter"] = forward_file["timeseries/$var_name/$iter"]
                end
            end
            

            # Copy over the filtered data, combined with backward data
            for var_name in filtered_var_names
                
                # Open the backward data as a FieldTimeSeries, so we can interpolate to match times
                fts_backward = FieldTimeSeries(backward_output_filename, var_name)

                # Loop over forward times and add the backward data
                for iter in forward_iterations
                    forward_time = forward_file["timeseries/t/$iter"]
                    forward_data = forward_file["timeseries/$var_name/$iter"] # Load in data
                    
                    # Write it again, adding the backward data using FieldTimeSeries interpolation. parent is used to strip offset from the backward data
                    combined_file["timeseries/$var_name/$iter"] = forward_data .+ parent(fts_backward[Time(T-forward_time)].data)
                end
            end

            # Mean velocities get subtracted instead
            for vel_name in filtered_vel_names
                
                # Open the backward data as a FieldTimeSeries, so we can interpolate to match times
                fts_backward = FieldTimeSeries(backward_output_filename, vel_name)

                # Loop over forward times and add the backward data
                for iter in forward_iterations
                    forward_time = forward_file["timeseries/t/$iter"]
                    forward_data = forward_file["timeseries/$vel_name/$iter"] # Load in data
                    
                    # Write it again, adding the backward data using FieldTimeSeries interpolation. parent is used to strip offset from the backward data
                    combined_file["timeseries/$vel_name/$iter"] = forward_data .- parent(fts_backward[Time(T-forward_time)].data)
                end
            end

        end
    end
    @info "Combined forward and backward contributions into $output_filename"

end

"""
    _remove_halos(data::AbstractArray, grid::AbstractGrid)

Removes the halo regions from a 3D data array based on the halo sizes specified
in the `grid` object.

Arguments
=========
- `data`: An `AbstractArray` representing the 3D data with halo regions.
- `grid`: An object containing the halo sizes `Hx`, `Hy`, and `Hz`.

Returns
=======
- A view of the `data` array with the halo regions removed.
"""
function _remove_halos(data::AbstractArray, grid::AbstractGrid)
    Hx = grid.Hx
    Hy = grid.Hy
    Hz = grid.Hz
    data = data[
                Hx != 0 ? (Hx+1:end-Hx) : (:),
                Hy != 0 ? (Hy+1:end-Hy) : (:),
                Hz != 0 ? (Hz+1:end-Hz) : (:)]
    return data
end

"""
    _fill_halos!(data::AbstractArray, grid::AbstractGrid, value::Real=0.0)

Fills the halo regions of a 3D array `data` with a specified `value`.

This function modifies the `data` array in-place. The dimensions of the halo regions
are determined by the `Hx`, `Hy`, and `Hz` fields of the `grid` object. 

# Arguments
- `data::AbstractArray`: The 3D array whose halo regions will be filled.
- `grid::AbstractGrid`: An object containing the dimensions of the halo regions (`Hx`, `Hy`, `Hz`).
- `value::Real=0.0`: The scalar value to use for filling the halo regions. Defaults to `0.0`.

# Returns
- `nothing`: This function does not return a value. It modifies the input array directly.

"""
function _fill_halos!(data::AbstractArray, grid::AbstractGrid, value::Real=0.0)
    Hx = grid.Hx
    Hy = grid.Hy
    Hz = grid.Hz
    data[1:Hx,:,:] .= value
    data[end-Hx+1:end,:,:] .= value
    data[:,1:Hy,:] .= value
    data[:,end-Hy+1:end,:] .= value
    data[:,:,1:Hz] .= value
    data[:,:,end-Hz+1:end] .= value
    return nothing
end

"""
    _create_coords(grid)

Creates a dictionary of coordinate arrays and mesh grids for a given `grid` object.

# Arguments
- `grid`: A grid object that defines the spatial dimensions and halo sizes.
  It must have fields `Nx`, `Ny`, `Nz`, `Hx`, `Hy`, and `Hz`.

# Returns
- `Dict`: A dictionary with the following keys:
    - `"full_grid_size"`: A tuple representing the dimensions of the full grid
      `(Nx + 2*Hx, Ny + 2*Hy, Nz + 2*Hz)`.
    - `"x"`, `"y"`, `"z"`: 1D `AbstractArray`s containing the `Center` coordinates along each axis,
      including halos.
    - `"x_mesh"`, `"y_mesh"`, `"z_mesh"`: 3D `AbstractArray`s representing the `Center` coordinate
      values at every point in the full grid. These are created by broadcasting the 1D
      coordinates to the full grid size.
"""
function _create_coords(grid::AbstractGrid)
    full_grid_size = (grid.Nx + 2*grid.Hx, grid.Ny+ 2*grid.Hy, grid.Nz+ + 2*grid.Hz)
    x = xnodes(grid, Center(), with_halos = true)
    y = ynodes(grid, Center(), with_halos = true)
    z = znodes(grid, Center(), with_halos = true)


    all_coords = (x=x,y=y,z=z)
    coord_dict = Dict()
    coord_dict["full_grid_size"] = full_grid_size
    for (coord_name, coord) in pairs(all_coords)
        if coord !== nothing
            coord_dict[String(coord_name)] = parent(coord)
            if coord_name == :x
                coord_dict["x_mesh"] = reshape(parent(coord),length(parent(coord)),1,1) .+ zeros(full_grid_size)
            elseif coord_name == :y
                coord_dict["y_mesh"] = reshape(parent(coord),1,length(parent(coord)),1) .+ zeros(full_grid_size)
            elseif coord_name == :z
                coord_dict["z_mesh"] = reshape(parent(coord),1,1,length(parent(coord))) .+ zeros(full_grid_size)
            end
        end
    end
    return coord_dict
end

"""
    _mask_immersed(data::AbstractArray, grid::AbstractGrid, Immersed::Bool, buffer_dz::Int = 3)
Applies masking to the input `data` array based on the presence of immersed boundaries in the `grid`. 
If `Immersed` is `true`, the function masks out regions below the bottom height of the immersed boundary, 
adding a buffer zone of `buffer_dz`*`dz` to avoid interpolation issues. RectilinearGrid is assumed.

Arguments
=========
- `data`: An instance of `AbstractArray` containing the array to be masked
- `grid`: An ImmersedBoundaryGrid grid object that defines the spatial dimensions and halo sizes.
- `data_has_halos`: A `Bool` indicating whether the input `data` includes halo regions. Defaults to `true`.
- `buffer_dz`: An `Int` specifying the number of grid cells to use as a buffer zone above the bottom height. Defaults to `3`.
"""
function _mask_immersed(data::AbstractArray, grid::ImmersedBoundaryGrid; fill_value::Float64 = NaN, data_has_halos::Bool = true, buffer_dz::Int = 0)
    # If there are immersed boundaries, we'll mask out the solid regions plus a small buffer zone with NaN (for field being regridded)
    # or 0 (for maps) to avoid bad interpolations
    if !data_has_halos # Halos have been removed already
        bottom_height = parent(grid.immersed_boundary.bottom_height)[grid.Hx+1:end-grid.Hx,grid.Hy+1:end-grid.Hy,:] # Remove halos
        z3D = _remove_halos(_create_coords(grid)["z_mesh"], grid)
        data[z3D .< bottom_height .+ buffer_dz*abs(grid.z.Δᵃᵃᶜ)] .= fill_value
    else # Data still has halos
        bottom_height = parent(grid.immersed_boundary.bottom_height)
        z3D = _create_coords(grid)["z_mesh"]
        data[z3D .< bottom_height .+ buffer_dz*abs(grid.z.Δᵃᵃᶜ)] .= fill_value
    end
    return data
end


"""
    regrid_to_mean_position!(config::AbstractConfig)

Regrids the filtered data to the mean position. This function reads the combined 
output file, interpolates the filtered variables to the mean position, and saves the
result in new variables within the same file.

The regridding process involves the following steps:
1.  **Extracts positions**: The mean positions (`xi_u`, `xi_v`, `xi_w`) and
    filtered variable data are extracted for each time step.
2.  **Handles periodicity**: For periodic dimensions (x, y, or z), the data is
    padded by repeating values near the boundaries to ensure accurate
    interpolation across the periodic boundaries.
3.  **Interpolates data**: A linear interpolator is used to map the filtered data
    from the irregular advected positions to the original, regular grid points.
4.  **Saves new fields**: The regridded data is saved as new variables in the
    combined output file, with a `_Lagrangian_filtered_at_mean` suffix.

Arguments
=========
- `config`: An instance of `AbstractConfig` containing the file paths, variable
  names, and grid information.
- `extra_vars_to_regrid::Tuple{Vararg{String}}=()`: Optional tuple of additional
  filtered variable names that have been calculated by the filter and also need to be
  regridded. Include velocities here if needed.
"""
function regrid_to_mean_position!(config::AbstractConfig; extra_vars_to_regrid::Tuple{Vararg{String}}=())
    #TODO split this function into smaller functions for readability
    output_filename = config.output_filename
    var_names_to_filter = config.var_names_to_filter
    compute_mean_velocities = config.compute_mean_velocities
    velocity_names = config.velocity_names
    npad = config.npad 
    label = config.label


    if (config isa AbstractOfflineConfig) && config.advection === nothing
        error("Regridding to mean position is not meaningful for Eulerian filtering")
    end
    if compute_mean_velocities
        var_names_to_filter = (var_names_to_filter..., velocity_names...)
    end
    
    # Get names of labelled mean variables
    var_names_to_regrid = Tuple([var * label * "_Lagrangian_filtered" for var in var_names_to_filter])

    # There might be some extra variables to regrid that the user defined
    var_names_to_regrid = Tuple(unique((var_names_to_regrid..., extra_vars_to_regrid...)))

    jldopen(output_filename,"r+") do file
        iterations = parse.(Int, keys(file["timeseries/t"]))
        grid = file["serialized/grid"]
        coord_dict = _create_coords(grid)

        # Check if it is an immersed boundary grid:
        if isa(grid, ImmersedBoundaryGrid)
            @warn "Grid is an ImmersedBoundaryGrid, regridding to mean position may not be sensible. 
            Areas below bottom height will be masked with NaNs, assuming bottom height is a function of x and/or y."
            Immersed = true
        else
            Immersed = false
        end

        # Error if not a RectilinearGrid
        if !(grid isa RectilinearGrid) && !((grid isa ImmersedBoundaryGrid) && (grid.underlying_grid isa RectilinearGrid))
            error("Regridding to mean position is only implemented for RectilinearGrid")
        end

        # Work out the periodic and bounded directions
        test_var = var_names_to_regrid[1]
        BCs = file["timeseries/$test_var/serialized/boundary_conditions"]
        periodic_dimensions = []
        bounded_dimensions = []
        if BCs.west == PeriodicBoundaryCondition()
            push!(periodic_dimensions,"x")
        elseif !Immersed && !isnothing(BCs.west)
            # We only do the fixed boundary correction if we know this is a non singleton dimension
            push!(bounded_dimensions,"x")
            @info "Assuming velocities normal to x boundaries are zero (open boundaries not yet supported for regridding)"
        end
        if BCs.south == PeriodicBoundaryCondition()
            push!(periodic_dimensions,"y")
        elseif !Immersed && !isnothing(BCs.south)   
            push!(bounded_dimensions,"y")
            @info "Assuming velocities normal to y boundaries are zero (open boundaries not yet supported for regridding)"
        end
        if BCs.top == PeriodicBoundaryCondition()
            push!(periodic_dimensions,"z")
        elseif !Immersed && !isnothing(BCs.top)
            push!(bounded_dimensions,"z")
            @info "Assuming velocities normal to z boundaries are zero (open boundaries not yet supported for regridding)"
        end
        
        # First add the necessary serialized entry for each new variable
        for var in var_names_to_regrid
            new_path = "timeseries/$var"*"_at_mean/serialized"
            if haskey(file, new_path)
                Base.delete!(file, new_path) #incase we already tried to write this
            end
            g = Group(file, new_path)
            for property in keys(file["timeseries/$var/serialized"])
                g[property] = file["timeseries/$var/serialized/$property"]
            end
        end

        # Now loop over time steps, extract the positions and data, and interpolate to the mean position
        for iter in iterations
            Xi_list = []
            regular_coord_mesh = []
            n_true_dims = 0
            true_dims = []
            for dim in ("x","y","z")
                if dim in keys(coord_dict) # This limits to only the non-singleton dimensions
                    n_true_dims +=1
                    push!(true_dims, dim)
                    push!(regular_coord_mesh, coord_dict["$(dim)_mesh"])
                    if (dim == "x") && ("u" in velocity_names)
                        if !Immersed
                            Xi_u = coord_dict["x_mesh"] .+ file["timeseries/xi_u"*label*"/$iter"]
                        else
                            Xi_u = coord_dict["x_mesh"] .+ _mask_immersed(file["timeseries/xi_u"*label*"/$iter"], grid, fill_value = 0.)
                        end
                        # Lose the halo regions (they don't help with fixed boundaries as they're zero, or with periodic as its repeated information)
                        Xi_u = _remove_halos(Xi_u,grid)

                        # move xi points outside of domain into domain
                        if "x" in periodic_dimensions                           
                            Xi_u .-= floor.((Xi_u .- grid.xᶠᵃᵃ[1])./grid.Lx) .* grid.Lx
                        end
                        push!(Xi_list,vec(Xi_u))
                        # Accompany this vector with a vector of x-indices for this data
                        # Get the size of the array
                        indices_array = [I[1] for I in CartesianIndices(size(Xi_u))]
                        push!(Xi_list,vec(indices_array))

                    elseif (dim == "x") && !("u" in velocity_names)
                        Xi_u = coord_dict["x_mesh"]

                        # Lose the halo regions 
                        Xi_u = _remove_halos(Xi_u,grid)
                        push!(Xi_list,vec(Xi_u))
                        indices_array = [I[1] for I in CartesianIndices(size(Xi_u))]
                        push!(Xi_list,vec(indices_array))

                    elseif (dim == "y") && ("v" in velocity_names)
                        if !Immersed
                            Xi_v =  coord_dict["y_mesh"] .+ file["timeseries/xi_v"*label*"/$iter"]
                        else
                            Xi_v =  coord_dict["y_mesh"] .+ _mask_immersed(file["timeseries/xi_v"*label*"/$iter"], grid, fill_value = 0.)
                        end

                        # Lose the halo regions 
                        Xi_v = _remove_halos(Xi_v,grid)

                        # move xi points outside of domain + halo regions into domain
                        if "y" in periodic_dimensions
                            Xi_v .-= floor.((Xi_v .- grid.yᵃᶠᵃ[1])./grid.Ly) .* grid.Ly
                        end
                        push!(Xi_list,vec(Xi_v))
                        indices_array = [I[2] for I in CartesianIndices(size(Xi_v))]
                        push!(Xi_list,vec(indices_array))

                    elseif (dim == "y") && !("v" in velocity_names)
                        Xi_v = coord_dict["y_mesh"]

                        # Lose the halo regions 
                        Xi_v = _remove_halos(Xi_v,grid)
                        push!(Xi_list,vec(Xi_v))
                        indices_array = [I[2] for I in CartesianIndices(size(Xi_v))]
                        push!(Xi_list,vec(indices_array))

                    elseif (dim == "z") && ("w" in velocity_names)
                        if !Immersed
                            Xi_w = coord_dict["z_mesh"] .+ file["timeseries/xi_w"*label*"/$iter"]
                        else
                            Xi_w = coord_dict["z_mesh"] .+ _mask_immersed(file["timeseries/xi_w"*label*"/$iter"], grid, fill_value = 0.)
                        end

                        # Lose the halo regions 
                        Xi_w = _remove_halos(Xi_w,grid)

                        # move xi points outside of domain + halo regions into domain
                        if "z" in periodic_dimensions
                            Xi_w .-= floor.((Xi_w .- grid.z.cᵃᵃᶠ[1])./grid.Lz) .* grid.Lz
                        end
                        push!(Xi_list,vec(Xi_w))
                        indices_array = [I[3] for I in CartesianIndices(size(Xi_w))]
                        push!(Xi_list,vec(indices_array))

                    elseif (dim == "z") && !("w" in velocity_names)
                        Xi_w = coord_dict["z_mesh"] .+ zeros(coord_dict["original_size"])

                        # Lose the halo regions 
                        Xi_w = _remove_halos(Xi_w,grid)
                        push!(Xi_list,vec(Xi_w))
                        indices_array = [I[3] for I in CartesianIndices(size(Xi_w))]
                        push!(Xi_list,vec(indices_array))

                    else
                        error("Something's wrong with the dimensions of the regridding routine")
                    end
                end
            
            end
            # Now we do some padding on the periodic dimensions, introducing new elements to the list near the periodic boundaries
            # First construct a matrix that contains the coordinates and the fields to interpolate
            for var in var_names_to_regrid
                if !Immersed
                    var_data = file["timeseries/$var/$iter"]
                else
                    var_data = _mask_immersed(file["timeseries/$var/$iter"], grid, fill_value = NaN)
                end
                
                # Lose the halo regions 
                var_data = _remove_halos(var_data,grid)

                push!(Xi_list, vec(var_data))
            end

            data_tuple = Tuple(Xi_list)
            data_array = hcat(data_tuple...) # This is a matrix where the rows are the data points and the columns are the coordinates then the variables

            # Then we take the array and repeat rows as necessary to add extra padding data
            column_number = 1
            n_columns = size(data_array,2)

            for dim in periodic_dimensions
                if dim == "x"
                    max_x = grid.xᶠᵃᵃ[grid.Nx+1]
                    min_x = grid.xᶠᵃᵃ[1]
                    xpad = npad*grid.Δxᶜᵃᵃ
                    Xi_x_vec = data_array[:,column_number] 
                    mask_max_x = (Xi_x_vec .> max_x - xpad) .& (Xi_x_vec .< max_x)
                    mask_min_x = (Xi_x_vec .< min_x + xpad) .& (Xi_x_vec .> min_x)
                    Xi_x_to_repeat = Xi_x_vec[mask_max_x .| mask_min_x]

                    # Remove or add Lx
                    Xi_x_to_repeat[(Xi_x_to_repeat .> max_x - xpad) .& (Xi_x_to_repeat .< max_x)] .-= grid.Lx
                    Xi_x_to_repeat[(Xi_x_to_repeat .< min_x + xpad) .& (Xi_x_to_repeat .> min_x)] .+= grid.Lx
                    extra_padding = fill(NaN, (length(Xi_x_to_repeat), n_columns))
                    extra_padding[:,column_number] = Xi_x_to_repeat

                    # And fill in the rest of the columns with straightforward repeateded data
                    for i in 1:n_columns
                        if i != column_number # Don't overwrite the x coordinate
                            extra_padding[:,i] = data_array[mask_max_x .| mask_min_x,i]
                        end
                    end

                    # Now join it on to the data array
                    data_array = vcat(data_array, extra_padding)

                    # Move along the rows to the next coordinate
                    column_number += 2 # We added an extra column of indices, so skip these

                elseif dim == "y"
                    max_y = grid.yᵃᶠᵃ[grid.Ny+1]
                    min_y = grid.yᵃᶠᵃ[1]
                    ypad = npad*grid.Δyᵃᶜᵃ
                    Xi_y_vec = data_array[:,column_number]
                    mask_max_y = (Xi_y_vec .> max_y - ypad) .& (Xi_y_vec .< max_y )
                    mask_min_y = (Xi_y_vec .< min_y + ypad) .& (Xi_y_vec .> min_y)   
                    Xi_y_to_repeat = Xi_y_vec[mask_max_y .| mask_min_y] 

                    # Remove or add Ly
                    Xi_y_to_repeat[(Xi_y_to_repeat .> max_y - ypad) .& (Xi_y_to_repeat .< max_y)] .-= grid.Ly
                    Xi_y_to_repeat[(Xi_y_to_repeat .< min_y + ypad) .& (Xi_y_to_repeat .> min_y)] .+= grid.Ly

                    
                    extra_padding = fill(NaN, (length(Xi_y_to_repeat), n_columns))
                    extra_padding[:,column_number] = Xi_y_to_repeat

                    # And fill in the rest of the columns with straightforward repeateded data
                    for i in 1:n_columns
                        if i != column_number # Don't overwrite the y coordinate
                            extra_padding[:,i] = data_array[mask_max_y .| mask_min_y,i]
                        end
                    end

                    # Now join it on to the data array
                    data_array = vcat(data_array, extra_padding)

                    # Move along to the next coordinate
                    column_number += 2 # We added an extra column of indices, so skip these
                elseif dim == "z"
                    max_z = grid.z.cᵃᵃᶠ[grid.Nz+1]
                    min_z = grid.z.cᵃᵃᶠ[1]
                    zpad = npad*grid.Δz.cᵃᵃᶜ
                    Xi_z_vec = data_array[:,column_number]
                    mask_max_z = (Xi_z_vec .> max_z - zpad) .& (Xi_z_vec .< max_z)
                    mask_min_z = (Xi_z_vec .< min_z + zpad) .& (Xi_z_vec .> min_z)
                    Xi_z_to_repeat = Xi_z_vec[mask_max_z .| mask_min_z]

                    # Remove or add Lz
                    Xi_z_to_repeat[(Xi_z_to_repeat .> max_z - zpad) .& (Xi_z_to_repeat .< max_z)] .-= grid.Lz
                    Xi_z_to_repeat[(Xi_z_to_repeat .< min_z + zpad) .& (Xi_z_to_repeat .> min_z)] .+= grid.Lz

                    extra_padding = fill(NaN, (length(Xi_z_to_repeat), n_columns))
                    extra_padding[:,column_number] = Xi_z_to_repeat

                    # And fill in the rest of the columns with straightforward repeateded data
                    for i in 1:n_columns
                        if i != column_number # Don't overwrite the z coordinate
                            extra_padding[:,i] = data_array[mask_max_z .| mask_min_z,i]
                        end
                    end
                    # Now join it on to the data array
                    data_array = vcat(data_array, extra_padding)
                    
                end
            end
            
            coords = data_array[:,1:2:2*n_true_dims] # The first columns are the coordinates and indices, just take the coordinates
            var_data = data_array[:,(2*n_true_dims+1):end] # The rest is the data
            
            # Normalisation to help with interpolation stability. Delaunay triangulation can struggle with very different scales in 
            # different dimensions (e.g. x - z slice of ocean).
            coord_mins = minimum(coords, dims=1)
            coord_maxs = maximum(coords, dims=1)
            coord_ranges = coord_maxs .- coord_mins
            coords_norm = (coords .- coord_mins) ./ coord_ranges
            
            regular_coord_mesh_norm = []
            for (i, mesh) in enumerate(regular_coord_mesh)
                norm_mesh = mesh .-coord_mins[i]
                norm_mesh = norm_mesh ./coord_ranges[i]
                push!(regular_coord_mesh_norm, norm_mesh)
            end

            for (ivar,var) in enumerate(var_names_to_regrid)    
                
                # This is the main interpolation
                values = var_data[:,ivar]
                interpolator = scipy_interpolate.LinearNDInterpolator(coords_norm, values)
                interp_data = pyconvert(Array,interpolator(regular_coord_mesh_norm...))

                # We already dealt with the periodic boundaries with padding, but we now make sure
                # that the interpolation is accurate at fixed boundaries by doing an interpolation 
                # at fixed coordinate normal to the boundary.
                # Reuse the data array using the indices columns to pull out the data we need. 
                for bounded_dim in bounded_dimensions # Just the non-singleton dimensions with fixed boundary
                    halo_size = getproperty(grid, Symbol("H$bounded_dim"))
                    true_dim_index = findfirst(isequal(bounded_dim), true_dims)
                    for edge_index in (1, getproperty(grid, Symbol("N$bounded_dim"))) # The index of the first and last real grid points in this dimension - halos have already been trimmed before indices assigned
                        edge_index_with_halos = edge_index + halo_size

                        # Filter to coordinates where the index in this dimension is edge_index
                        data_array_cut = data_array[Int.(data_array[:,2*true_dim_index]) .== Int(edge_index),:] 
                        
                        # Now remove the columns with the bounded coordinate
                        data_array_cut = hcat(data_array_cut[:, 1:2*true_dim_index-2], data_array_cut[:, 2*true_dim_index+1:end])
                        
                        # Then sort data_array_cut by the first remaining coordinate - this is needed for 1D interpolation
                        data_array_cut = sortslices(data_array_cut, dims=1, by=row -> row[1])

                        # data_array_cut can now be used for interpolation in one fewer dimension
                        coords_cut = data_array_cut[:,1:2:2*(n_true_dims-1)] # The first columns are the coordinates and indices, just take the coordinates
                        values_cut = data_array_cut[:,2*(n_true_dims-1)+ivar] # The rest is the data
                        
                        # Normalise coords_cut for interpolation stability
                        coord_mins_cut = minimum(coords_cut, dims=1)
                        coord_maxs_cut = maximum(coords_cut, dims=1)
                        coord_ranges_cut = coord_maxs_cut .- coord_mins_cut
                        coords_cut_norm = (coords_cut .- coord_mins_cut) ./ coord_ranges_cut
                        if bounded_dim == "x"
                            if n_true_dims == 2
                                # No need to normalise coords_cut as we are only doing 1D interpolation here
                                if "y" in true_dims
                                    mesh = coord_dict["y_mesh"][edge_index_with_halos,:,1]
                                    interp_data[edge_index_with_halos,:,1] = pyconvert(Array,numpy.interp(mesh, coords_cut[:,1], values_cut))
                                elseif "z" in true_dims
                                    mesh = coord_dict["z_mesh"][edge_index_with_halos,1,:]
                                    interp_data[edge_index_with_halos,1,:] = pyconvert(Array,numpy.interp(mesh, coords_cut[:,1], values_cut))
                                else
                                    @info "Dimension combo $true_dims is not implemented"
                                end
                                
                            elseif n_true_dims == 3
                                # Need to normalise for interpolation stability
                                y_mesh = coord_dict["y_mesh"][edge_index_with_halos,:,:]
                                z_mesh = coord_dict["z_mesh"][edge_index_with_halos,:,:]
                                y_mesh_norm = (y_mesh .- coord_mins_cut[2]) ./ coord_ranges_cut[2]
                                z_mesh_norm = (z_mesh .- coord_mins_cut[3]) ./ coord_ranges_cut[3]
                                meshes = (y_mesh_norm,z_mesh_norm)
                                interpolator = scipy_interpolate.LinearNDInterpolator(coords_cut_norm, values_cut)
                                interp_data[edge_index_with_halos,:,:] = pyconvert(Array,interpolator(meshes...))
                            else
                                @info "Number of dimensions $n_true_dims is not implemented"
                            end
            
                        elseif bounded_dim =="y"
                            if n_true_dims == 2
                                # No need to normalise coords_cut as we are only doing 1D interpolation here
                                if "x" in true_dims
                                    mesh = coord_dict["x_mesh"][:,edge_index_with_halos,1]
                                    interp_data[:,edge_index_with_halos,1] = pyconvert(Array,numpy.interp(mesh, coords_cut[:,1], values_cut))
                                elseif "z" in true_dims
                                    mesh = coord_dict["z_mesh"][1,edge_index_with_halos,:]
                                    interp_data[1,edge_index_with_halos,:] = pyconvert(Array,numpy.interp(mesh, coords_cut[:,1], values_cut))
                                else
                                    @info "Dimension combo $true_dims is not implemented"
                                end
                                
                                
                            elseif n_true_dims == 3
                                # Need to normalise for interpolation stability
                                x_mesh = coord_dict["x_mesh"][:,edge_index_with_halos,:]
                                z_mesh = coord_dict["z_mesh"][:,edge_index_with_halos,:]
                                x_mesh_norm = (x_mesh .- coord_mins_cut[1]) ./ coord_ranges_cut[1]
                                z_mesh_norm = (z_mesh .- coord_mins_cut[3]) ./ coord_ranges_cut[3]
                                meshes = (x_mesh_norm,z_mesh_norm)
                                interpolator = scipy_interpolate.LinearNDInterpolator(coords_cut_norm, values_cut)
                                interp_data[:,edge_index_with_halos,:] = pyconvert(Array,interpolator(meshes...))
                            else
                                @info "Number of dimensions $n_true_dims is not implemented"
                            end
                        elseif bounded_dim == "z"
                            if n_true_dims == 2
                                # No need to normalise coords_cut as we are only doing 1D interpolation here
                                if "x" in true_dims
                                    mesh = coord_dict["x_mesh"][:,1,edge_index_with_halos]
                                    interp_data[:,1,edge_index_with_halos] = pyconvert(Array,numpy.interp(mesh, coords_cut[:,1], values_cut))                           
                                elseif "y" in true_dims
                                    mesh = coord_dict["y_mesh"][1,:,edge_index_with_halos]
                                    interp_data[1,:,edge_index_with_halos] = pyconvert(Array,numpy.interp(mesh, coords_cut[:,1], values_cut))
                                else
                                    @info "Dimension combo $true_dims is not implemented"
                                end
                                
                            elseif n_true_dims == 3
                                # Need to normalise for interpolation stability
                                x_mesh = coord_dict["x_mesh"][:,:,edge_index_with_halos]
                                y_mesh = coord_dict["y_mesh"][:,:,edge_index_with_halos]
                                x_mesh_norm = (x_mesh .- coord_mins_cut[1]) ./ coord_ranges_cut[1]
                                y_mesh_norm = (y_mesh .- coord_mins_cut[2]) ./ coord_ranges_cut[2]
                                meshes = (x_mesh_norm,y_mesh_norm)
                                interpolator = scipy_interpolate.LinearNDInterpolator(coords_cut_norm, values_cut)
                                interp_data[:,:,edge_index_with_halos] = pyconvert(Array,interpolator(meshes...))
                            else
                                @info "Number of dimensions $n_true_dims is not implemented"
                            end
                        end
                    end 

                end
                
                # Now we make sure the halo regions are filled with zeros again
                _fill_halos!(interp_data, grid, 0.0)
                
                # Finally write the data to a new variable in the file
                new_var_loc = "timeseries/$var"*"_at_mean/$iter"
                if haskey(file, new_var_loc)
                    Base.delete!(file, new_var_loc) #incase we already tried to write this variable
                end
                file[new_var_loc] = interp_data
                
            end
        end
    end
    @info "Wrote regridded data to new variables with _at_mean suffix in file $output_filename"
    
end

"""
    jld2_to_netcdf(jld2_filename::String, nc_filename::String)

Converts a JLD2 output file generated by an Oceananigans simulation into a
standard NetCDF file. This function is useful for post-processing and for
sharing data with other tools that expect the NetCDF format.

The conversion process involves the following steps:
1.  **Read JLD2 data**: Opens the input JLD2 file and reads the grid, time,
    and all timeseries variables.
2.  **Create NetCDF file**: Creates a new NetCDF file with a `.nc` extension.
3.  **Define dimensions**: Defines NetCDF dimensions based on the grid sizes
    and staggered locations (e.g., `x_caa` for cell centers, `x_faa` for
    cell faces).
4.  **Define grid variables**: Writes the grid coordinates and metadata
    (e.g., `Lx`, `Ny`, `Hx`) as variables to the NetCDF file.
5.  **Write timeseries data**: Iterates through each variable in the JLD2
    file's timeseries, determines its location on the grid, and writes the
    data to a new variable in the NetCDF file.
6.  **Add metadata**: Adds attributes to each variable, including boundary
    conditions and units, for better documentation.

Arguments
=========
- `jld2_filename`: A `String` specifying the path to the input JLD2 file.
- `nc_filename`: A `String` specifying the path for the output NetCDF file.
"""
function jld2_to_netcdf(jld2_filename::String, nc_filename::String)
    jldopen(jld2_filename, "r") do file
        
        iterations = parse.(Int, keys(file["timeseries/t"]))
        times = [file["timeseries/t/$iter"] for iter in iterations]
        dt = times[2] - times[1]
        grid = file["serialized/grid"]

        rm(nc_filename, force=true)
        ds = NCDataset(nc_filename,"c", attrib = OrderedDict(
            "Julia"                     => "This file was generated using Julia Version $VERSION",

        ))

        # Dimensions
        ds.dim["time"] = length(times)

        if (grid isa RectilinearGrid) || ((grid isa ImmersedBoundaryGrid) && (grid.underlying_grid isa RectilinearGrid))
            ds.dim["y_afa"] = try; length(grid.yᵃᶠᵃ) catch; 1 end
            ds.dim["x_faa"] = try; length(grid.xᶠᵃᵃ) catch; 1 end
            ds.dim["x_caa"] = try; length(grid.xᶜᵃᵃ) catch;1 end
            ds.dim["y_aca"] = try; length(grid.yᵃᶜᵃ) catch; 1 end
            ds.dim["z_aaf"] = try; length(grid.z.cᵃᵃᶠ) catch; 1 end
            ds.dim["z_aac"] = try; length(grid.z.cᵃᵃᶜ) catch; 1 end
        # LatitudeLongitudeGrid has different names for the coordinate arrays
        elseif (grid isa LatitudeLongitudeGrid) || ((grid isa ImmersedBoundaryGrid) && (grid.underlying_grid isa LatitudeLongitudeGrid))
            ds.dim["lat_afa"] = try; length(grid.φᵃᶠᵃ) catch; 1 end
            ds.dim["lon_faa"] = try; length(grid.λᶠᵃᵃ) catch; 1 end
            ds.dim["lon_caa"] = try; length(grid.λᶜᵃᵃ) catch;1 end
            ds.dim["lat_aca"] = try; length(grid.φᵃᶜᵃ) catch; 1 end
            ds.dim["z_aaf"] = try; length(grid.z.cᵃᵃᶠ) catch; 1 end
            ds.dim["z_aac"] = try; length(grid.z.cᵃᵃᶜ) catch; 1 end

        else 
            error("Grid type $(typeof(grid)) not supported for NetCDF conversion")
        end
        
        # Declare and fill time variables

        nctime = defVar(ds,"time", Float64, ("time",), attrib = OrderedDict(
            "units"                     => "seconds",
            "long_name"                 => "Time",
        ))
        nctime[:] = times

        # There might be a time shifted variable
        if haskey(file["timeseries"], "t_shifted")
            t_shifted = [file["timeseries/t_shifted/$iter"] for iter in iterations]
            nctime = defVar(ds,"time_shifted", Float64, ("time",), attrib = OrderedDict(
            "units"                     => "seconds",
            "long_name"                 => "Time shifted to mean time",
            ))
            nctime[:] = t_shifted
        end

        # Declare grid variables
        if (grid isa RectilinearGrid) || ((grid isa ImmersedBoundaryGrid) && (grid.underlying_grid isa RectilinearGrid))
            ncy_afa = defVar(ds,"y_afa", Float32, ("y_afa",), attrib = OrderedDict(
                "units"                     => "m",
                "long_name"                 => "Cell face locations in the y-direction.",
            ))
            

            ncx_faa = defVar(ds,"x_faa", Float32, ("x_faa",), attrib = OrderedDict(
                "units"                     => "m",
                "long_name"                 => "Cell face locations in the x-direction.",
            ))
            

            ncx_caa = defVar(ds,"x_caa", Float32, ("x_caa",), attrib = OrderedDict(
                "units"                     => "m",
                "long_name"                 => "Cell center locations in the x-direction.",
            ))
            

            ncy_aca = defVar(ds,"y_aca", Float32, ("y_aca",), attrib = OrderedDict(
                "units"                     => "m",
                "long_name"                 => "Cell center locations in the y-direction.",
            ))
            

            ncz_aac = defVar(ds,"z_aac", Float32, ("z_aac",), attrib = OrderedDict(
                "units"                     => "m",
                "long_name"                 => "Cell center locations in the z-direction.",
            ))
            

            ncz_aaf = defVar(ds,"z_aaf", Float32, ("z_aaf",), attrib = OrderedDict(
                "units"                     => "m",
                "long_name"                 => "Cell face locations in the z-direction.",
            ))
            

            ncdx_caa = defVar(ds,"dx_caa", Float32, (), attrib = OrderedDict(
                "units"                     => "m",
                "long_name"                 => "Spacing between cell faces (located at the cell centers) in the x-direction.",
            ))
            

            ncdx_faa = defVar(ds,"dx_faa", Float32, (), attrib = OrderedDict(
                "units"                     => "m",
                "long_name"                 => "Spacing between cell centers (located at the cell faces) in the x-direction.",
            ))
            

            ncdy_aca = defVar(ds,"dy_aca", Float32, (), attrib = OrderedDict(
                "units"                     => "m",
                "long_name"                 => "Spacing between cell faces (located at cell centers) in the y-direction.",
            ))
            

            ncdy_afa = defVar(ds,"dy_afa", Float32, (), attrib = OrderedDict(
                "units"                     => "m",
                "long_name"                 => "Spacing between cell centers (located at cell faces) in the y-direction.",
            ))
            
            ncdz_aac = defVar(ds,"dz_aac", Float32, (), attrib = OrderedDict(
                "units"                     => "m",
                "long_name"                 => "Spacing between cell faces (located at cell centers) in the z-direction.",
            ))
            

            ncdz_aaf = defVar(ds,"dz_aaf", Float32, (), attrib = OrderedDict(
                "units"                     => "m",
                "long_name"                 => "Spacing between cell centers (located at cell faces) in the z-direction.",
            ))

            # Fill grid variables

            ncx_faa[:] = try; parent(grid.xᶠᵃᵃ) catch; 0 end
            ncy_afa[:] = try; parent(grid.yᵃᶠᵃ) catch; 0 end
            ncx_caa[:] = try; parent(grid.xᶜᵃᵃ) catch; 0 end
            ncy_aca[:] = try; parent(grid.yᵃᶜᵃ) catch; 0 end
            ncz_aac[:] = try; parent(grid.z.cᵃᵃᶜ) catch; 0 end
            ncz_aaf[:] = try; parent(grid.z.cᵃᵃᶠ) catch; 0 end
            ncdx_caa[:] = try; parent(grid.Δxᶜᵃᵃ) catch; 0 end
            ncdx_faa[:] = try; parent(grid.Δxᶠᵃᵃ) catch; 0 end
            ncdy_aca[:] = try; parent(grid.Δyᵃᶜᵃ) catch; 0 end
            ncdy_afa[:] = try; parent(grid.Δyᵃᶠᵃ) catch; 0 end
            ncdz_aac[:] = try; parent(grid.z.Δᵃᵃᶜ) catch; 0 end
            ncdz_aaf[:] = try; parent(grid.z.Δᵃᵃᶠ) catch; 0 end

        elseif (grid isa LatitudeLongitudeGrid) || ((grid isa ImmersedBoundaryGrid) && (grid.underlying_grid isa LatitudeLongitudeGrid))
            nclat_afa = defVar(ds,"lat_afa", Float32, ("lat_afa",), attrib = OrderedDict(
                "units"                     => "degrees_north",
                "long_name"                 => "Cell face locations in the latitude direction.",
            ))
            

            nclon_faa = defVar(ds,"lon_faa", Float32, ("lon_faa",), attrib = OrderedDict(
                "units"                     => "degrees_east",
                "long_name"                 => "Cell face locations in the longitude direction.",
            ))
            

            nclon_caa = defVar(ds,"lon_caa", Float32, ("lon_caa",), attrib = OrderedDict(
                "units"                     => "degrees_east",
                "long_name"                 => "Cell center locations in the longitude direction.",
            ))
            

            nclat_aca = defVar(ds,"lat_aca", Float32, ("lat_aca",), attrib = OrderedDict(
                "units"                     => "degrees_north",
                "long_name"                 => "Cell center locations in the latitude direction.",
            ))
            

            ncz_aac = defVar(ds,"z_aac", Float32, ("z_aac",), attrib = OrderedDict(
                "units"                     => "m",
                "long_name"                 => "Cell center locations in the z-direction.",
            ))
            

            ncz_aaf = defVar(ds,"z_aaf", Float32, ("z_aaf",), attrib = OrderedDict(
                "units"                     => "m",
                "long_name"                 => "Cell face locations in the z-direction.",
            ))
            
            # ncdx_cfa = defVar(ds,"dx_cfa", Float32, ("lat_afa",), attrib = OrderedDict(
            #     "units"                     => "m",
            #     "long_name"                 => "Spacing between cell faces (located at the cell centers in x and faces in y) in the x-direction.",
            # ))
            

            # ncdx_ffa = defVar(ds,"dx_ffa", Float32, ("lat_afa",), attrib = OrderedDict(
            #     "units"                     => "m",
            #     "long_name"                 => "Spacing between cell centres (located at the cell faces in x and y) in the x-direction.",
            # ))

            ncdx_fca = defVar(ds,"dx_fca", Float32, ("lat_aca",), attrib = OrderedDict(
                "units"                     => "m",
                "long_name"                 => "Spacing between cell centres (located at the cell faces in x and centers in y) in the x-direction.",
            ))
            

            ncdx_cca = defVar(ds,"dx_cca", Float32, ("lat_aca",), attrib = OrderedDict(
                "units"                     => "m",
                "long_name"                 => "Spacing between cell faces (located at the cell centers in x and y) in the x-direction.",
            ))
            

            ncdy_fca = defVar(ds,"dy_fca", Float32, (), attrib = OrderedDict(
                "units"                     => "m",
                "long_name"                 => "Spacing between cell centers (located at cell faces in x and centers in y) in the y-direction.",
            ))
            

            ncdy_cfa = defVar(ds,"dy_cfa", Float32, (), attrib = OrderedDict(
                "units"                     => "m",
                "long_name"                 => "Spacing between cell faces (located at cell centers in x and faces in y) in the y-direction.",
            ))
            
            ncdz_aac = defVar(ds,"dz_aac", Float32, (), attrib = OrderedDict(
                "units"                     => "m",
                "long_name"                 => "Spacing between cell faces (located at cell centers) in the z-direction.",
            ))
            

            ncdz_aaf = defVar(ds,"dz_aaf", Float32, (), attrib = OrderedDict(
                "units"                     => "m",
                "long_name"                 => "Spacing between cell centers (located at cell faces) in the z-direction.",
            ))

            # Fill grid variables

            nclon_faa[:] = try; parent(grid.λᶠᵃᵃ) catch ; 0 end
            nclat_afa[:] = try; parent(grid.φᵃᶠᵃ) catch; 0 end
            nclon_caa[:] = try; parent(grid.λᶜᵃᵃ) catch; 0 end
            nclat_aca[:] = try; parent(grid.φᵃᶜᵃ) catch; 0 end
            ncz_aac[:] = try; parent(grid.z.cᵃᵃᶜ) catch; 0 end
            ncz_aaf[:] = try; parent(grid.z.cᵃᵃᶠ) catch; 0 end
            # ncdx_cfa[:] = try; parent(grid.Δxᶜᶠᵃ) catch; 0 end # We should include these and figure out what dimensions they live on
            # ncdx_ffa[:] = try; parent(grid.Δxᶠᶠᵃ) catch; 0 end
            ncdx_cca[:] = try; parent(grid.Δxᶜᶜᵃ) catch; 0 end
            ncdx_fca[:] = try; parent(grid.Δxᶠᶜᵃ) catch; 0 end
            ncdy_fca[:] = try; parent(grid.Δyᶠᶜᵃ) catch; 0 end
            ncdy_cfa[:] = try; parent(grid.Δyᶜᶠᵃ) catch; 0 end
            ncdz_aac[:] = try; parent(grid.z.Δᵃᵃᶜ) catch; 0 end
            ncdz_aaf[:] = try; parent(grid.z.Δᵃᵃᶠ) catch; 0 end
        
        else 
            error("Grid type $(typeof(grid)) not supported for NetCDF conversion")
        end

        # Define dimensions - the same for rectilinear and lat-lon grids
        ncNx = defVar(ds,"Nx", Int64, (), attrib = OrderedDict(
            "units"                     => "None",
            "long_name"                 => "Number of cells in the x-direction.",
        ))

        ncNx[:] = grid.Nx

        ncNy = defVar(ds,"Ny", Int64, (), attrib = OrderedDict(
            "units"                     => "None",
            "long_name"                 => "Number of cells in the y-direction.",
        ))
        ncNy[:] = grid.Ny


        ncNz = defVar(ds,"Nz", Int64, (), attrib = OrderedDict(
            "units"                     => "None",
            "long_name"                 => "Number of cells in the z-direction.",
        ))
        ncNz[:] = grid.Nz
        # Define halos

        ncHx = defVar(ds,"Hx", Int64, (), attrib = OrderedDict(
            "units"                     => "None",
            "long_name"                 => "Number of halo cells in the x-direction.",
        ))
        ncHx[:] = grid.Hx

        ncHy = defVar(ds,"Hy", Int64, (), attrib = OrderedDict(
            "units"                     => "None",
            "long_name"                 => "Number of halo cells in the y-direction.",
        ))
        ncHy[:] = grid.Hy

        ncHz = defVar(ds,"Hz", Int64, (), attrib = OrderedDict(
            "units"                     => "None",
            "long_name"                 => "Number of halo cells in the z-direction.",
        ))
        ncHz[:] = grid.Hz

        # Define dimension lengths

        ncLx = defVar(ds,"Lx", Float64, (), attrib = OrderedDict(
            "units"                     => "m",
            "long_name"                 => "Length of the domain in the x-direction.",
        ))
        ncLx[:] = grid.Lx
        ncLy = defVar(ds,"Ly", Float64, (), attrib = OrderedDict(
            "units"                     => "m",
            "long_name"                 => "Length of the domain in the y-direction.",
        ))
        ncLy[:] = grid.Ly   
        ncLz = defVar(ds,"Lz", Float64, (), attrib = OrderedDict(
            "units"                     => "m",
            "long_name"                 => "Length of the domain in the z-direction.",
        ))
        ncLz[:] = grid.Lz

        # And define location of variables
        if (grid isa RectilinearGrid) || ((grid isa ImmersedBoundaryGrid) && (grid.underlying_grid isa RectilinearGrid))
         
            location_map = Dict(
                (Center,Center,Center) => ("x_caa","y_aca","z_aac"),
                (Face,Center,Center)   => ("x_faa","y_aca","z_aac"),
                (Center,Face,Center)   => ("x_caa","y_afa","z_aac"),
                (Face,Face,Center)     => ("x_faa","y_afa","z_aac"),
                (Center,Center,Face)   => ("x_caa","y_aca","z_aaf"),
                (Face,Center,Face)     => ("x_faa","y_aca","z_aaf"),
                (Center,Face,Face)     => ("x_caa","y_afa","z_aaf"),
                (Face,Face,Face)       => ("x_faa","y_afa","z_aaf"),
            )
        elseif (grid isa LatitudeLongitudeGrid) || ((grid isa ImmersedBoundaryGrid) && (grid.underlying_grid isa LatitudeLongitudeGrid))
            location_map = Dict(
                (Center,Center,Center) => ("lon_caa","lat_aca","z_aac"),
                (Face,Center,Center)   => ("lon_faa","lat_aca","z_aac"),
                (Center,Face,Center)   => ("lon_caa","lat_afa","z_aac"),
                (Face,Face,Center)     => ("lon_faa","lat_afa","z_aac"),
                (Center,Center,Face)   => ("lon_caa","lat_aca","z_aaf"),
                (Face,Center,Face)     => ("lon_faa","lat_aca","z_aaf"),
                (Center,Face,Face)     => ("lon_caa","lat_afa","z_aaf"),
                (Face,Face,Face)       => ("lon_faa","lat_afa","z_aaf"),
            )
        else 
            error("Grid type $(typeof(grid)) not supported for NetCDF conversion")
        end

        for varname in keys(file["timeseries"])
            
            if varname ∉ ("t","t_shifted") 

                # Create a variable in the NetCDF file
                bc_string = sprint(show, file["timeseries/$varname/serialized/boundary_conditions"])
                ncv = defVar(ds, varname, Float64, (location_map[file["timeseries/$varname/serialized/location"]]...,"time"), attrib = OrderedDict(
                    "boundary conditions"                     => bc_string,
                ))
                
                for (it,iter) in enumerate(iterations)
                    # Assign data to the variable
                    ncv[:, :, :, it] = file["timeseries/$varname/$iter"]
                end
                
            end
        end

        close(ds)
        
    end
    @info "Wrote NetCDF file to $nc_filename"
end

"""
    get_weight_function(;t::AbstractArray, tref::Real, filter_params::NamedTuple, direction::String = "both")

Computes the weighting function for the offline filter. This function calculates
the filter's impulse response, which determines how much each point in the
timeseries `t` contributes to the filtered value at a reference time `tref`.
The weighting function is based on the provided `filter_params`, which contains
the coefficients for the filter's impulse response.

Keyword arguments
=========
- `t`: A collection of time points in the timeseries.
- `tref`: The reference time at which the filter is being evaluated.
- `filter_params`: A `NamedTuple` containing the coefficients (`a`, `b`, `c`,
  `d`) and the number of coefficient pairs (`N_coeffs`).
- `direction`: A `String` indicating the direction of the filter. It can be
  "both" (default), "forward", or "backward". This determines whether the
  filter is applied symmetrically around `tref`, only to past times, or only
  to future times.

Returns
=======
- A vector of weights `G`, with the same dimensions as `t`, representing the
  value of the filter's impulse response at each time point relative to `tref`.
"""
function get_weight_function(;t::AbstractArray, tref::Real, filter_params::NamedTuple, direction::String = "both")

    G = 0*t
    N_coeffs = filter_params.N_coeffs
    if N_coeffs == 0.5
        a1 = filter_params.a1
        c1 = filter_params.c1
        G .= a1.*exp.(-c1.*abs.(t .- tref))
    else
        for i in 1:N_coeffs
            
            a = getproperty(filter_params, Symbol("a$i"))
            b = getproperty(filter_params, Symbol("b$i"))
            c = getproperty(filter_params, Symbol("c$i"))
            d = getproperty(filter_params, Symbol("d$i"))

            G += (a.*cos.(d.*abs.(t .- tref)) .+ b.*sin.(d.*abs.(t .- tref))).*exp.(-c.*abs.(t .- tref))
        end
    end
    if direction == "forward"
        G[t .> tref] .= 0
    elseif direction == "backward"
        G[t .< tref] .= 0
    elseif direction != "both"
        error("Direction must be 'forward', 'backward' or 'both'")
    end
    return G
end

"""
    get_offline_frequency_response(;freq::AbstractArray, filter_params::NamedTuple)

Calculates the frequency response of the offline filter. This function takes a set
of frequencies and the filter's coefficients to compute how the filter amplifies
or attenuates different frequency components of a signal.

The response is computed by summing the contributions of each coefficient pair
based on the filter's transfer function in the frequency domain. The result is
a measure of the filter's gain at each given frequency.

Arguments
=========
- `freq`: A vector of frequencies (in radians per unit time).
- `filter_params`: A `NamedTuple` containing the filter coefficients (`a`, `b`,
  `c`, `d`) and the number of coefficient pairs (`N_coeffs`).

Returns
=======
- A vector `Ghat` representing the filter's frequency response at each
  corresponding frequency in `freq`.
"""
function get_offline_frequency_response(;freq::AbstractArray, filter_params::NamedTuple)
    
    Ghat = 0*freq
    N_coeffs = filter_params.N_coeffs
 
    if N_coeffs == 0.5
        a1 = filter_params.a1
        c1 = filter_params.c1
        Ghat .= (2.0*a1*c1)./(c1^2 .+ freq.^2)
        return Ghat
    else

        for i in 1:N_coeffs
            
            a = getproperty(filter_params, Symbol("a$i"))
            b = getproperty(filter_params, Symbol("b$i"))
            c = getproperty(filter_params, Symbol("c$i"))
            d = getproperty(filter_params, Symbol("d$i"))

            Ghat += (a*c .+ b.*(d .+ freq))./(c^2 .+ (d .+ freq).^2) .+ (a*c .+ b.*(d .- freq))./(c^2 .+ (d .- freq).^2)
        end
    
    return Ghat
    end
end

"""
    get_online_frequency_response(;freq::AbstractArray, filter_params::NamedTuple)

Calculates the frequency response of the online filter (i.e a one-sided filter that 
is zero for negative times). This function takes a set of frequencies and the filter's 
coefficients to compute how the filter amplifies or attenuates different frequency 
components of a signal.

The response is computed by summing the contributions of each coefficient pair
based on the filter's transfer function in the frequency domain. The result is
a measure of the filter's gain at each given frequency.

Arguments
=========
- `freq`: A vector of frequencies (in radians per unit time).
- `filter_params`: A `NamedTuple` containing the filter coefficients (`a`, `b`,
  `c`, `d`) and the number of coefficient pairs (`N_coeffs`).

Returns
=======
- A complex vector `Ghat` representing the filter's frequency response at each
  corresponding frequency in `freq`.
"""
function get_online_frequency_response(;freq::AbstractArray, filter_params::NamedTuple)
    
    Ghat = 0*freq .+ 0im
    N_coeffs = filter_params.N_coeffs
 
    if N_coeffs == 0.5
        a1 = filter_params.a1
        c1 = filter_params.c1
        Ghat .= a1./(c1 .+ im.*freq)
        return Ghat
    else

        for i in 1:N_coeffs
            
            a = getproperty(filter_params, Symbol("a$i"))
            b = getproperty(filter_params, Symbol("b$i"))
            c = getproperty(filter_params, Symbol("c$i"))
            d = getproperty(filter_params, Symbol("d$i"))

            Ghat += 0.5*((a .+ im.*b)./(c .+ im.*(d .+ freq)) .+ (a .- im.*b)./(c .+ im.*(-d .+ freq)))
        end
    
    return Ghat
    end
end

"""
    compute_Eulerian_filter!(config::AbstractConfig)

Computes the Eulerian filter for specified variables and writes the results to a
combined output file. This function performs a direct, convolution-style
filtering of a time series by applying a weighting function to the data at each
time step.

The function iterates through each variable to be filtered:
1.  **Reads data**: The entire time series of the variable is read from the
    JLD2 file.
2.  **Applies weighting**: At each output time, a weighting function `G` is
    computed and applied to the entire time series. The weighted data is summed
    to produce the filtered field.
3.  **Writes output**: The resulting filtered field is saved back to the
    same JLD2 file in a new group with the `_Eulerian_filtered` suffix.

This method serves as a benchmark for comparison with the main Lagrangian filter.

The method uses data saved to the filter output file - incase we decide to save this
at lower frequency than the original data, it should be rewritten to use the original
data file instead.

Arguments
=========
- `config`: An instance of `AbstractConfig` containing the file path, variable
  names, and filter parameters.
"""
function compute_Eulerian_filter!(config::AbstractConfig)
    isnothing(config.cutoff_mask) ||
        error("compute_Eulerian_filter! does not support a spatial cutoff_mask")
    filter_params = config.filter_params
    output_filename = config.output_filename
    var_names_to_filter = config.var_names_to_filter
    compute_mean_velocities = config.compute_mean_velocities
    velocity_names = config.velocity_names
    label = config.label

    var_names_to_Eulerian_filter = var_names_to_filter
    if compute_mean_velocities
        var_names_to_Eulerian_filter = (var_names_to_Eulerian_filter..., velocity_names...)
    end
    
    if (config isa AbstractOfflineConfig)
        direction = "both"
    elseif (config isa AbstractOnlineConfig)
        direction = "forward"
    end

    # Open existing file
    jldopen(output_filename,"r+") do file
        iterations = parse.(Int, keys(file["timeseries/t"]))
        times = [file["timeseries/t/$iter"] for iter in iterations]
        dt = times[2] - times[1] # Initialise time interval

        # Loop over variables to filter
        for var_name in var_names_to_Eulerian_filter
            @info "Computing Eulerian filter for variable $var_name"

            # Create group for filtered data
            g_EF = Group(file, "timeseries/$(var_name * label *"_Eulerian_filtered")")

            # Copy over serialized properties
            g_EF_serialized = Group(file, "timeseries/$(var_name * label * "_Eulerian_filtered")/serialized") 
            for property in keys(file["timeseries/$var_name/serialized"])
                g_EF_serialized[property] = file["timeseries/$var_name/serialized/$property"]
            end

            # Loop over times to compute filtered field at each time
            for (i, t) in enumerate(times)
                G = get_weight_function(t = times, tref = t, filter_params = filter_params, direction = direction)
                
                # Initialise with zeros
                mean_field = file["timeseries/$(var_name)/$(iterations[1])"]*0.0

                normalisation = sum(G[1:end-1] .* diff(times))
                # Construct mean sequentially
                for j in 1:length(times)
                    field = file["timeseries/$(var_name)/$(iterations[j])"]
                    dt = j < length(times) ? times[j+1] - times[j] : dt
                    mean_field .+= G[j] .* field .* dt 
                end
                g_EF["$(iterations[i])"] = mean_field./normalisation
            end
        end
    end
end

"""
    compute_time_shift!(config::AbstractConfig)

Computes the time shift for a forward filter (either online or forward-only offline)
based on its coefficients and writes the shifted time series to the output file.

The time shift is computed as the time delay introduced by the filter's transfer function. 
This new time series is stored in a new group called `timeseries/t_shifted` within the output JLD2 file.

# Arguments
- `config`: A configuration object of type `AbstractFilterConfig` which contains
  the `output_filename` and `filter_params` (filter coefficients).

"""
function compute_time_shift!(config::AbstractConfig)
    isnothing(config.cutoff_mask) ||
        error("compute_time_shift! has no single physical-time shift for a spatial cutoff_mask")
    if !(config isa AbstractOnlineConfig)
        @warn "Time shift computation is only relevant when filtering forward only. Offline forward-backward filtering
        has an even weight function, so time shift should be zero. This function will compute a time shift regardless, but 
        it may not be meaningful for offline forward-backward filters."
    end
    output_filename = config.output_filename
    filter_params = config.filter_params    
    N_coeffs = filter_params.N_coeffs
    time_shift = 0.0
    if N_coeffs == 0.5 # exponential special case
        time_shift = 1/filter_params.c1
    else
        for i in 1:N_coeffs
            a = getproperty(filter_params, Symbol("a$i"))
            b = getproperty(filter_params, Symbol("b$i"))
            c = getproperty(filter_params, Symbol("c$i"))
            d = getproperty(filter_params, Symbol("d$i"))
            time_shift += (a*c^2 + 2*b*c*d - a*d^2)/(c^2 + d^2)^2
        end
    end
    jldopen(output_filename,"r+") do file
        iterations = parse.(Int, keys(file["timeseries/t"]))
        times = [file["timeseries/t/$iter"] for iter in iterations]
        t_shift_group = JLD2.Group(file, "timeseries/t_shifted")

        for (i, t) in enumerate(times)
            t_shift_group["$(iterations[i])"] = t - time_shift
        end

    end
    @info "Wrote time shift data to new group timeseries/t_shifted in file $output_filename"

end